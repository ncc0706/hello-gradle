import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.32.0'
}

apply plugin: 'signing'
apply plugin: 'com.vanniktech.maven.publish'

group 'io.github.ncc0706'
version "0.0.1-SNAPSHOT"

repositories {
}

dependencies {

    implementation 'commons-codec:commons-codec:1.18.0'
    testImplementation 'junit:junit:4.12'
}

tasks.register('sourceJar', Jar) {
    archiveClassifier="sources"
    from sourceSets.main.allJava
}


tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

javadoc {
    description = "生成jar格式的javadoc。"
    // 只显示 protected 和 public 的类和成员
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.header = project.name
    // 静默javadoc检查（比如不支持@date会报错等），jdk 8+
    options.addStringOption('Xdoclint:none', '-quiet')
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
    // suppress warnings due to cross-module @see and @link references;
    // note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    // suppress "## warnings" message
    logging.captureStandardOutput LogLevel.INFO
    // 编码一定要配置否则直接出错
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
}


mavenPublishing {
    // mavenCentral snapshot repository(新的)
    // https://central.sonatype.com/repository/maven-snapshots
    // https://central.sonatype.com/service/rest/repository/browse/maven-snapshots/
    // 必须使用高版本才支持（0.31.0 支持上传 snapshot）
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)

    // Enable GPG signing for all publications
    signAllPublications()

    // 添加 pom 相关信息
    // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
    pom {
        name = "hello-gradle"
        description = "使用gradle发布项目到中央仓库"
        url = "https://github.com/ncc0706/hello-gradle"
        licenses {
            license {
                name = "The MIT License"
                url = "https://github.com/ncc0706/hello-gradle/master/LICENSE"
            }
        }
        developers {
            developer {
                id = "ncc0706"
                name = "ncc0706"
                email = "ncc0706@gmail.com"
            }
        }
        scm {
            connection = "scm:git:https://github.com/ncc0706/hello-gradle.git"
            developerConnection = "scm:git:https://github.com/ncc0706/hello-gradle.git"
            url = "https://github.com/ncc0706/hello-gradle"
        }
    }
}

wrapper {
    distributionUrl = 'https://mirrors.huaweicloud.com/gradle/gradle-8.8-bin.zip'
}
